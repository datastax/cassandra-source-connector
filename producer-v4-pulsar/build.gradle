plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow"
}

application {
    mainClass.set(project.getProperty("mainClassName"))
}


jar {
    manifest {
        attributes 'Premain-Class': "$mainClassName"
    }
    zip64=true
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

jar.enabled = false
assemble.dependsOn(shadowJar)

compileTestJava {
    options.compilerArgs += '-parameters'
}

dependencies {
    implementation project(':commons')
    implementation project(':producer')
    implementation project(':producer-v4')
    implementation("io.debezium:debezium-core:${debeziumVersion}")
    implementation("org.apache.pulsar:pulsar-client:${pulsarVersion}")
    compileOnly("org.apache.cassandra:cassandra-all:${cassandra4Version}")
    testImplementation("org.apache.cassandra:cassandra-all:${cassandra4Version}") {
        exclude group: 'io.netty'
    }

    testImplementation "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "com.datastax.oss:java-driver-core:${ossDriverVersion}"
    testImplementation "com.datastax.oss:java-driver-query-builder:${ossDriverVersion}"

    testImplementation project(':testcontainers')

    testRuntimeOnly"org.slf4j:slf4j-api:1.7.30"
    testRuntimeOnly"org.slf4j:slf4j-simple:1.7.30"
}

test {
    // Add dependency on jar task, since it will be main target for testing
    dependsOn shadowJar

    // Rearrange test classpath, add compiled JAR instead of main classes
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(shadowJar.archiveFile)

    useJUnitPlatform()

    systemProperty "buildDir", buildDir
    systemProperty "projectVersion", project.version
}


